#version 330 core

uniform mat4 myview_matrix;
uniform mat4 mymodel_matrix;
uniform mat3 mynormal_matrix;

in vec4 myvertex;
in vec3 mynormal;
in vec2 texCoord;

layout (location = 0) out vec4 gPosition;
layout (location = 1) out vec4 gAlbedo;
layout (location = 2) out vec4 gNormal;
layout (location = 3) out vec4 gEnv;

uniform sampler2D texAlbedo;
uniform sampler2D texAO;
uniform sampler2D texHeight;
uniform sampler2D texMetal;
uniform sampler2D texNormal;
uniform sampler2D texRough;

uniform samplerCube cubetex;

//http://www.opengl-tutorial.org/intermediate-tutorials/tutorial-13-normal-mapping/
vec3 detailedTexNormal(vec3 viewNormal, vec3 tNormal, vec3 position)
{
    vec3 dPosX  = dFdx(position.xyz);
    vec3 dPosY  = dFdy(position.xyz);
    vec2 dTexX = dFdx(texCoord);
    vec2 dTexY = dFdy(texCoord);

    vec3 normal = normalize(viewNormal);
    vec3 tangent = normalize(dPosX * dTexY.t - dPosY * dTexX.t);
    vec3 binormal = -normalize(cross(normal, tangent));
    mat3 TBN = mat3(tangent, binormal, normal);

    return normalize(TBN * tNormal);
}

void main (void)
{   
	vec4 _pos = myview_matrix * mymodel_matrix * myvertex;
	vec3 mypos = (_pos.xyz) / _pos.w;

	gPosition = vec4(mypos, 1.0);
	gPosition.a = texture(texAO, texCoord.st).r;
	gAlbedo.rgb = texture(texAlbedo, texCoord.st).rgb;
	gAlbedo.a = texture(texRough, texCoord.st).r;

	vec3 tNormal = normalize(texture(texNormal, texCoord.st).rgb * 2.0f - 1.0f);
	gNormal.rgb = detailedTexNormal(mynormal_matrix * mynormal, tNormal, mypos.rgb);
	gNormal.a = texture(texMetal, texCoord.st).r;

	vec3 eyepos = vec3(0,0,0);
    vec3 normal = normalize(mynormal);
	vec3 mypos_to_eyepos = normalize(eyepos - mypos);
	vec3 reflection = reflect(-mypos_to_eyepos, normal);

	vec3 index = inverse(mynormal_matrix) * reflection;

	gEnv.rgb = texture(cubetex, index).rgb;
	gEnv.a = 1.0f;
}